// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import { ZoneInterface } from "seaport-types/src/interfaces/ZoneInterface.sol";

import { ERC165 } from "@openzeppelin/contracts/utils/introspection/ERC165.sol";
import {
    ERC721Interface
} from "seaport-types/src/interfaces/AbridgedTokenInterfaces.sol";

import { ItemType } from "seaport-types/src/lib/ConsiderationEnums.sol";

import {
    ReceivedItem,
    Schema,
    ZoneParameters
} from "seaport-types/src/lib/ConsiderationStructs.sol";

contract TestPostExecution is ERC165, ZoneInterface {
    function authorizeOrder(
        ZoneParameters calldata
    ) public pure returns (bytes4) {
        return this.authorizeOrder.selector;
    }

    function validateOrder(
        ZoneParameters calldata zoneParameters
    ) external view override returns (bytes4 validOrderMagicValue) {
        if (zoneParameters.consideration.length == 0) {
            revert("No consideration items supplied");
        }

        ReceivedItem memory receivedItem = zoneParameters.consideration[0];

        address currentOwner;
        try
            ERC721Interface(receivedItem.token).ownerOf(receivedItem.identifier)
        returns (address owner) {
            currentOwner = owner;
        } catch {
            revert("Unsupported consideration token type (must implement 721)");
        }

        if (receivedItem.itemType != ItemType.ERC721) {
            revert("Validity check performed with unsupported item type");
        }

        // Note that endAmount has been repurposed as recipient; this interface
        // still needs to be modified to return spent / received items.
        if (receivedItem.amount != 1) {
            // Note that this is currently failing in the matchOrder case.
            revert("Returned item amount incorrectly modified");
        }

        if (currentOwner != receivedItem.recipient) {
            revert("Validity check performed prior to execution");
        }

        validOrderMagicValue = ZoneInterface.validateOrder.selector;
    }

    /**
     * @dev Returns the metadata for this zone.
     */
    function getSeaportMetadata()
        external
        pure
        override
        returns (
            string memory name,
            Schema[] memory schemas // map to Seaport Improvement Proposal IDs
        )
    {
        schemas = new Schema[](1);
        schemas[0].id = 3003;
        schemas[0].metadata = new bytes(0);

        return ("TestPostExecution", schemas);
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view override(ERC165, ZoneInterface) returns (bool) {
        return
            interfaceId == type(ZoneInterface).interfaceId ||
            super.supportsInterface(interfaceId);
    }
}
