// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import { Result } from "./FuzzHelpers.sol";
import "seaport-sol/SeaportSol.sol";

struct FuzzParams {
    uint256 seed;
}

struct ReturnValues {
    bool fulfilled;
    bool cancelled;
    bool validated;
    bool[] availableOrders;
    Execution[] executions;
}

interface TestLike {
    function getMatchedFulfillments(
        AdvancedOrder[] memory orders
    )
        external
        returns (
            Fulfillment[] memory fulfillments,
            MatchComponent[] memory remainingOfferComponents,
            MatchComponent[] memory remainingConsiderationComponents
        );
}

struct TestContext {
    /**
     * @dev An array of AdvancedOrders
     */
    AdvancedOrder[] orders;
    /**
     * @dev A Seaport interface, either the reference or optimized version.
     */
    SeaportInterface seaport;
    /**
     * @dev A ConduitController interface.
     */
    ConduitControllerInterface conduitController;
    /**
     * @dev A caller address. If this is nonzero, the FuzzEngine will prank this
     *      address before calling exec.
     */
    address caller;
    /**
     * @dev A struct containing fuzzed params generated by the Foundry fuzzer.
     *      Right now these params include only a uint256 seed, which we could
     *      potentially use to generate other random data.
     */
    FuzzParams fuzzParams;
    /**
     * @dev An array of function selectors for "checks". The FuzzEngine will
     *      call these functions after calling exec to make assertions about
     *      the resulting test state.
     */
    bytes4[] checks;
    /**
     * @dev Additional data we might need to fulfill an order. This is basically
     *      the superset of all the non-order args to SeaportInterface
     *      functions, like conduit key, criteria resolvers, and fulfillments.
     *      if you don't want to set these parameters every time, use
     *      TestContextLib.from() to create a TestContext with these fields
     *      pre-populated with empty defaults.
     */
    uint256 counter;
    bytes32 fulfillerConduitKey;
    bytes32[] expectedZoneCalldataHash;
    CriteriaResolver[] criteriaResolvers;
    address recipient;
    Fulfillment[] fulfillments;
    FulfillmentComponent[][] offerFulfillments;
    FulfillmentComponent[][] considerationFulfillments;
    uint256 maximumFulfilled;
    BasicOrderParameters basicOrderParameters;
    /**
     * @dev A copy of the original orders array. Use this to make assertions
     *      about the final state of the orders after calling exec. This is
     *      automatically copied if you use the TestContextLib.from() function.
     */
    AdvancedOrder[] initialOrders;
    /**
     * @dev Expected Result state for each order. Indexes correspond to the
     *      indexes of the orders in the orders array.
     */
    Result[] expectedResults;
    /**
     * @dev Return values from the last call to exec. Superset of return values
     *      from all Seaport functions.
     */
    ReturnValues returnValues;
    TestLike testHelpers;
}

/**
 * @notice Builder library for TestContext.
 */
library TestContextLib {
    using AdvancedOrderLib for AdvancedOrder;
    using AdvancedOrderLib for AdvancedOrder[];
    using BasicOrderParametersLib for BasicOrderParameters;

    /**
     * @dev Create an empty TestContext.
     *
     * @custom:return emptyContext the empty TestContext
     */
    function empty() internal pure returns (TestContext memory) {
        return
            TestContext({
                orders: new AdvancedOrder[](0),
                seaport: SeaportInterface(address(0)),
                conduitController: ConduitControllerInterface(address(0)),
                caller: address(0),
                fuzzParams: FuzzParams({ seed: 0 }),
                checks: new bytes4[](0),
                counter: 0,
                fulfillerConduitKey: bytes32(0),
                expectedZoneCalldataHash: new bytes32[](0),
                criteriaResolvers: new CriteriaResolver[](0),
                recipient: address(0),
                fulfillments: new Fulfillment[](0),
                offerFulfillments: new FulfillmentComponent[][](0),
                considerationFulfillments: new FulfillmentComponent[][](0),
                maximumFulfilled: 0,
                basicOrderParameters: BasicOrderParametersLib.empty(),
                initialOrders: new AdvancedOrder[](0),
                expectedResults: new Result[](0),
                returnValues: ReturnValues({
                    fulfilled: false,
                    cancelled: false,
                    validated: false,
                    availableOrders: new bool[](0),
                    executions: new Execution[](0)
                }),
                testHelpers: TestLike(address(0))
            });
    }

    /**
     * @dev Create a TestContext from the given partial arguments.
     *
     * @param orders the AdvancedOrder[] to set
     * @param seaport the SeaportInterface to set
     * @param caller the caller address to set
     * @param fuzzParams the fuzzParams struct to set
     * @custom:return _context the TestContext
     */
    function from(
        AdvancedOrder[] memory orders,
        SeaportInterface seaport,
        address caller,
        FuzzParams memory fuzzParams
    ) internal pure returns (TestContext memory) {
        return
            TestContext({
                orders: orders,
                seaport: seaport,
                conduitController: ConduitControllerInterface(address(0)),
                caller: caller,
                fuzzParams: fuzzParams,
                checks: new bytes4[](0),
                counter: 0,
                fulfillerConduitKey: bytes32(0),
                expectedZoneCalldataHash: new bytes32[](0),
                criteriaResolvers: new CriteriaResolver[](0),
                recipient: address(0),
                fulfillments: new Fulfillment[](0),
                offerFulfillments: new FulfillmentComponent[][](0),
                considerationFulfillments: new FulfillmentComponent[][](0),
                maximumFulfilled: 0,
                basicOrderParameters: BasicOrderParametersLib.empty(),
                initialOrders: orders.copy(),
                expectedResults: new Result[](0),
                returnValues: ReturnValues({
                    fulfilled: false,
                    cancelled: false,
                    validated: false,
                    availableOrders: new bool[](0),
                    executions: new Execution[](0)
                }),
                testHelpers: TestLike(address(0))
            });
    }

    /**
     * @dev Sets the orders on a TestContext
     *
     * @param context the TestContext to set the orders of
     * @param orders the AdvancedOrder[] to set
     *
     * @return _context the TestContext with the orders set
     */
    function withOrders(
        TestContext memory context,
        AdvancedOrder[] memory orders
    ) internal pure returns (TestContext memory) {
        context.orders = orders.copy();
        return context;
    }

    /**
     * @dev Sets the SeaportInterface on a TestContext
     *
     * @param context the TestContext to set the SeaportInterface of
     * @param seaport the SeaportInterface to set
     *
     * @return _context the TestContext with the SeaportInterface set
     */
    function withSeaport(
        TestContext memory context,
        SeaportInterface seaport
    ) internal pure returns (TestContext memory) {
        context.seaport = seaport;
        return context;
    }

    /**
     * @dev Sets the ConduitControllerInterface on a TestContext
     *
     * @param context the TestContext to set the ConduitControllerInterface of
     * @param conduitController the ConduitControllerInterface to set
     *
     * @return _context the TestContext with the ConduitControllerInterface set
     */
    function withConduitController(
        TestContext memory context,
        ConduitControllerInterface conduitController
    ) internal pure returns (TestContext memory) {
        context.conduitController = conduitController;
        return context;
    }

    /**
     * @dev Sets the caller on a TestContext
     *
     * @param context the TestContext to set the caller of
     * @param caller the caller address to set
     *
     * @return _context the TestContext with the caller set
     */
    function withCaller(
        TestContext memory context,
        address caller
    ) internal pure returns (TestContext memory) {
        context.caller = caller;
        return context;
    }

    /**
     * @dev Sets the fuzzParams on a TestContext
     *
     * @param context the TestContext to set the fuzzParams of
     * @param fuzzParams the fuzzParams struct to set
     *
     * @return _context the TestContext with the fuzzParams set
     */
    function withFuzzParams(
        TestContext memory context,
        FuzzParams memory fuzzParams
    ) internal pure returns (TestContext memory) {
        context.fuzzParams = _copyFuzzParams(fuzzParams);
        return context;
    }

    /**
     * @dev Sets the checks on a TestContext
     *
     * @param context the TestContext to set the checks of
     * @param checks the checks array to set
     *
     * @return _context the TestContext with the checks set
     */
    function withChecks(
        TestContext memory context,
        bytes4[] memory checks
    ) internal pure returns (TestContext memory) {
        context.checks = _copyBytes4(checks);
        return context;
    }

    /**
     * @dev Sets the counter on a TestContext
     *
     * @param context the TestContext to set the counter of
     * @param counter the counter value to set
     *
     * @return _context the TestContext with the counter set
     */
    function withCounter(
        TestContext memory context,
        uint256 counter
    ) internal pure returns (TestContext memory) {
        context.counter = counter;
        return context;
    }

    /**
     * @dev Sets the fulfillerConduitKey on a TestContext
     *
     * @param context the TestContext to set the fulfillerConduitKey of
     * @param fulfillerConduitKey the fulfillerConduitKey value to set
     *
     * @return _context the TestContext with the fulfillerConduitKey set
     */
    function withFulfillerConduitKey(
        TestContext memory context,
        bytes32 fulfillerConduitKey
    ) internal pure returns (TestContext memory) {
        context.fulfillerConduitKey = fulfillerConduitKey;
        return context;
    }

    /**
     * @dev Sets the criteriaResolvers on a TestContext
     *
     * @param context the TestContext to set the criteriaResolvers of
     * @param criteriaResolvers the criteriaResolvers array to set
     *
     * @return _context the TestContext with the criteriaResolvers set
     */
    function withCriteriaResolvers(
        TestContext memory context,
        CriteriaResolver[] memory criteriaResolvers
    ) internal pure returns (TestContext memory) {
        context.criteriaResolvers = _copyCriteriaResolvers(criteriaResolvers);
        return context;
    }

    /**
     * @dev Sets the recipient on a TestContext
     *
     * @param context the TestContext to set the recipient of
     * @param recipient the recipient value to set
     *
     * @return _context the TestContext with the recipient set
     */
    function withRecipient(
        TestContext memory context,
        address recipient
    ) internal pure returns (TestContext memory) {
        context.recipient = recipient;
        return context;
    }

    /**
     * @dev Sets the fulfillments on a TestContext
     *
     * @param context the TestContext to set the fulfillments of
     * @param fulfillments the offerFulfillments value to set
     *
     * @return _context the TestContext with the fulfillments set
     */
    function withFulfillments(
        TestContext memory context,
        Fulfillment[] memory fulfillments
    ) internal pure returns (TestContext memory) {
        context.fulfillments = fulfillments;
        return context;
    }

    /**
     * @dev Sets the offerFulfillments on a TestContext
     *
     * @param context the TestContext to set the offerFulfillments of
     * @param offerFulfillments the offerFulfillments value to set
     *
     * @return _context the TestContext with the offerFulfillments set
     */
    function withOfferFulfillments(
        TestContext memory context,
        FulfillmentComponent[][] memory offerFulfillments
    ) internal pure returns (TestContext memory) {
        context.offerFulfillments = _copyFulfillmentComponents(
            offerFulfillments
        );
        return context;
    }

    /**
     * @dev Sets the considerationFulfillments on a TestContext
     *
     * @param context the TestContext to set the considerationFulfillments of
     * @param considerationFulfillments the considerationFulfillments value to set
     *
     * @return _context the TestContext with the considerationFulfillments set
     */
    function withConsiderationFulfillments(
        TestContext memory context,
        FulfillmentComponent[][] memory considerationFulfillments
    ) internal pure returns (TestContext memory) {
        context.considerationFulfillments = _copyFulfillmentComponents(
            considerationFulfillments
        );
        return context;
    }

    /**
     * @dev Sets the maximumFulfilled on a TestContext
     *
     * @param context the TestContext to set the maximumFulfilled of
     * @param maximumFulfilled the maximumFulfilled value to set
     *
     * @return _context the TestContext with maximumFulfilled set
     */
    function withMaximumFulfilled(
        TestContext memory context,
        uint256 maximumFulfilled
    ) internal pure returns (TestContext memory) {
        context.maximumFulfilled = maximumFulfilled;
        return context;
    }

    /**
     * @dev Sets the basicOrderParameters on a TestContext
     *
     * @param context the TestContext to set the fulfillments of
     * @param basicOrderParameters the offerFulfillments value to set
     *
     * @return _context the TestContext with the fulfillments set
     */
    function withBasicOrderParameters(
        TestContext memory context,
        BasicOrderParameters memory basicOrderParameters
    ) internal pure returns (TestContext memory) {
        context.basicOrderParameters = basicOrderParameters;
        return context;
    }

    function _copyBytes4(
        bytes4[] memory selectors
    ) private pure returns (bytes4[] memory) {
        bytes4[] memory copy = new bytes4[](selectors.length);
        for (uint256 i = 0; i < selectors.length; i++) {
            copy[i] = selectors[i];
        }
        return copy;
    }

    function _copyFulfillmentComponents(
        FulfillmentComponent[][] memory fulfillmentComponents
    ) private pure returns (FulfillmentComponent[][] memory) {
        FulfillmentComponent[][]
            memory outerCopy = new FulfillmentComponent[][](
                fulfillmentComponents.length
            );
        for (uint256 i = 0; i < fulfillmentComponents.length; i++) {
            FulfillmentComponent[]
                memory innerCopy = new FulfillmentComponent[](
                    fulfillmentComponents[i].length
                );
            for (uint256 j = 0; j < fulfillmentComponents[i].length; j++) {
                innerCopy[j] = fulfillmentComponents[i][j];
            }
            outerCopy[i] = innerCopy;
        }
        return outerCopy;
    }

    function _copyCriteriaResolvers(
        CriteriaResolver[] memory criteriaResolvers
    ) private pure returns (CriteriaResolver[] memory) {
        CriteriaResolver[] memory copy = new CriteriaResolver[](
            criteriaResolvers.length
        );
        for (uint256 i = 0; i < criteriaResolvers.length; i++) {
            copy[i] = criteriaResolvers[i];
        }
        return copy;
    }

    function _copyFuzzParams(
        FuzzParams memory params
    ) private pure returns (FuzzParams memory) {
        return FuzzParams({ seed: params.seed });
    }
}
